apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

def pfaFile = rootProject.file('pfa.properties')

android {
    allprojects {
        tasks.withType(JavaCompile){
            options.compilerArgs <<"-Xlint:deprecation"
        }
    }

    signingConfigs {
        if (pfaFile.exists()) {
            'pfa-key' {
                def pfa = new Properties()
                pfa.load(new FileInputStream(pfaFile))

                storePassword pfa['storePassword']
                keyAlias pfa['keyAlias']
                keyPassword pfa['keyPassword']
                storeFile file(pfa['storeFile'])
            }
        }
    }

    compileSdkVersion 33
    defaultConfig {
        multiDexEnabled true
        applicationId 'org.secuso.privacyfriendlyactivitytracker'
        minSdkVersion 31
        targetSdkVersion 33
        versionCode 15
        versionName "3.0.1"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "pfa-pedometer-${variant.name}-v${variant.versionName}.apk"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
        }
        debug {
            debuggable true
            if (pfaFile.exists()) {
                signingConfig signingConfigs.'pfa-key'
            }
        }
    }
    productFlavors {
    }
    namespace 'org.secuso.privacyfriendlyactivitytracker'
}

repositories {
    maven { url "https://jitpack.io" }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')


    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:3.12.4'


    androidTestImplementation 'androidx.test.ext:junit:1.2.0-alpha01'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.0-alpha01'

    implementation 'com.google.gms:google-services:4.3.15'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.0-beta1'
    androidTestImplementation 'androidx.test:rules:1.4.1-alpha05'
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.tracing:tracing:1.1.0'

    def work_version = "2.8.1"
    implementation "androidx.work:work-runtime:$work_version"
    implementation "androidx.work:work-runtime-ktx:$work_version"
    androidTestImplementation "androidx.work:work-testing:$work_version"

    implementation "androidx.sqlite:sqlite-ktx:2.3.1"
    implementation 'androidx.sqlite:sqlite-framework:2.3.1'
}
